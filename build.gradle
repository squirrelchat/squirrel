buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

apply plugin: 'application'
mainClassName = 'chat.squirrel.SquirrelMain'
def versionObj = new Version(major: 0, minor: 0, revision: 1)

apply plugin: 'com.github.johnrengelman.shadow'

allprojects {
    group 'chat.squirrel'
    version versionObj.toString()
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'io.vertx:vertx-web:3.8.4'
    
    implementation 'io.sentry:sentry-logback:1.7.22'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'io.lettuce:lettuce-core:5.1.6.RELEASE'
    implementation 'org.mongodb:mongo-java-driver:3.8.2'
    implementation 'org.javadelight:delight-metrics:0.0.6'
}

import org.apache.tools.ant.filters.ReplaceTokens

//noinspection GroovyAssignabilityCheck
task sourcesForRelease(type: Copy) {
    outputs.upToDateWhen { false }

    from('src/main/java') {
        include '**/SquirrelVersion.java'
        filter(ReplaceTokens, tokens: [
                VERSION_MAJOR   : versionObj.major,
                VERSION_MINOR   : versionObj.minor,
                VERSION_REVISION: versionObj.revision,
                COMMIT          : getCommitHash(),
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

//noinspection GroovyAssignabilityCheck
task generateJavaSources(type: SourceTask) {
    outputs.upToDateWhen { false }

    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'SquirrelVersion.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    outputs.upToDateWhen { false }

    source = generateJavaSources.source
    dependsOn generateJavaSources
}

shadowJar {
    version = versionObj.toString()
}

class Version {
    String major, minor, revision

    String toString() {
        "$major.$minor.$revision"
    }
}

static def getCommitHash() {
    def p = Runtime.getRuntime().exec("git rev-parse HEAD")
    p.waitFor()
    p.getIn().text.trim()
}